// Width and Height in Terminal (generally 8:20)
width = 150
height = 40

// Max Number of Iterations
max_iter = 200

plane_x = -3
plane_y = -2
plane_width = 6  
plane_height = 4

// Iterating Function
cr,ci,zr,zi,iter,tr
mandelbrot = {
    // initialize variables
    cr = @[0] * plane_width / width + plane_x
    ci = @[1] * plane_height / height + plane_y

    zr = 0; zi = 0
    iter = 0

    // iteration
    std.repeat({
        tr = zr * zr - zi * zi + cr
        zi = 2 * zi * zr + ci
        zr = tr

        // increase count
        iter = iter + 1

        // check
        (zi * zi + zr * zr <= 4) && (iter < max_iter)
    }) 
    
    // return amount of iterations needed (not finished if max_iter)
    (iter)
}

// Function to map Numbers to ASCII Characters
characters = " .'`^\",:;Il!i><~+_-?][}{1)(|\\/tfjrxnuvczXYUJCLQ0OZmwqpdbkhao*#MW&8%B@$"
characters_len = std.len(characters)
ascii = {
    characters[(std.floor(@[0] / max_iter * (characters_len - 1)))]
}

// Iterate over all Pixels in the Terminal
y = 0
std.repeat({
    x = 0
    std.repeat({
        value = mandelbrot(x, y)
        std.print(ascii(value)) 
        x = x + 1
        (x < width)
    })
    std.print("\n")
    y = y + 1
    (y < height)
})
